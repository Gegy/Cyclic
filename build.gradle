buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

import net.minecraftforge.gradle.common.tasks.SignJar
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

version = "${mc_version}-${mod_version}"
group = 'com.lothrazar.cyclic' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'Cyclic'

// java.toolchain.languageVersion = JavaLanguageVersion.of(8) 
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    
    mappings channel: 'snapshot', version: mappings_version
		// TODO:
		//     mappings channel: 'official', version: '1.16.5'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

						// probably for patchouli
            property 'mixin.env.disableRefMap', 'true'
		  
            mods {
                cyclic {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.disableRefMap', 'true'

            mods {
                cyclic {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
// sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    
    implementation fg.deobf("mezz.jei:${jei_version}")
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
    implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-${mc_version}:${crafttweaker_version}")
    
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")

    compileOnly fg.deobf("vazkii.botania:Botania:${botania_version}:api")
    runtimeOnly fg.deobf("vazkii.botania:Botania:${botania_version}")

    // optional dependencies & mods for testing compat

 implementation fg.deobf("curse.maven:jade-324717:3910873")
 
    implementation fg.deobf("curse.maven:mantle-74924:3482897")
    implementation fg.deobf("curse.maven:tinkers-construct-74072:3482903")
    implementation fg.deobf("curse.maven:cucumber-272335:3507886")
    implementation fg.deobf("curse.maven:mystical-agriculture-246640:3562127")
    implementation fg.deobf("curse.maven:chunknogobyebye-332695:3195333")
    implementation fg.deobf("curse.maven:yungs-api-421850:3494919")
    implementation fg.deobf("curse.maven:yungs-better-strongholds-465575:3412586")
    implementation fg.deobf("curse.maven:v-tweaks-238048:3675562")
    implementation fg.deobf("curse.maven:mekanism-268560:3659389")

    // To use any mod in /libs/ folder, place it in and set the mod and version to match jar filename
   // runtimeOnly fg.deobf("libs:SimpleStorageNetwork:1.16.4-1.3.1")
   // runtimeOnly fg.deobf("libs:refinedstorage:1.16.4-1.9.11")
    
    // You may put jars on which you depend on in ./libs or you may define them like so..
    // compile "some.group:artifact:version:classifier"
    // compile "some.group:artifact:version"

    // For more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

repositories {
 // if you want local jar files loading in use this
 // below. and above in deps use something like runtimeOnly fg.deobf("libs:SimpleStorageNetwork:1.16.4-1.3.1")
  flatDir {
  	dirs 'libs'
  }
  maven {
    name = "mappings"
    url 'https://dogforce-games.com/maven'
  }
  maven { url 'https://www.cursemaven.com'  }
  maven { url = "https://dvs1.progwml6.com/files/maven/"  }
  maven { url = "https://maven.theillusivec4.top/"    }
  maven { url 'https://maven.blamejared.com' }    
}
// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": "cyclic",
            "Specification-Vendor": "cyclicsareus",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"cyclicsareus",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar') 
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

task signJar(type: SignJar, dependsOn: jar) {

    // findProperty allows us to reference the property without it existing. 
    // Using project.propName would cause the script to fail validation if 
    // the property did not exist. 
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

task cleanJar {
  delete 'build/libs'
}
